; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

; Suche macht nur auf 64-Bit-System Sinn
if ($INST_SystemType$ = "64 Bit System") and (($INST_architecture$ = "system specific") or ($INST_architecture$ = "both") or ($INST_architecture$ = "64 only"))

	Message "Searching installation of " + $PrettyName$ + " 64 Bit..."
	Set $SearchPattern$ = $PrettyName$
	; Parameter: $SearchPattern$ Suchbegriff in Registry
	Sub_search_registry64_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge

	if ( count ($ResultList$) = "0" )
		comment "No installations of " + $SearchPattern$ + " found in registry. Nothing to do ;-) "
	else
		Message "Uninstalling " + $PrettyName$ + " 64 Bit..."
		for %s% in $ResultList$ do Sub_uninstall_64
	endif
endif

Message "Searching installation of " + $PrettyName$ + " 32 Bit..."
Set $SearchPattern$ = $PrettyName$
; Parameter: $SearchPattern$ Suchbegriff in Registry
Sub_search_registry32_uninstall_keys
; Rückgabewert: $ResultList$ gefundene Einträge

if ( count ($ResultList$) = "0" )
	comment "No installations of " + $SearchPattern$ + " found in registry. Nothing to do ;-) "
else
	Message "Uninstalling " + $PrettyName$ + " 32 Bit..."
	for %s% in $ResultList$ do Sub_uninstall_32
endif
			
[Sub_uninstall_64]
Set $UninstallProgram$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\%s%] UninstallString")
; Anführungsstriche in Registry-Pfad entfernen, da sie stören
Set $UninstallProgram$ = takeString (1, splitString ( $UninstallProgram$ , '"'))
Set $InstallDir$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\%s%] InstallLocation")
if ($INST_architecture$ = "both")
	Set $ProductName$ = $PrettyName$ + " (64 bit)"			
else
	Set $ProductName$ = $PrettyName$
endif			
Winbatch_uninstall
sub_check_exitcode
			
set $DesktopIcon$ = getProductProperty("desktopicon","no")
if $DesktopIcon$ = "yes"
	comment "Delete common desktop icon"
	Linkfolder_uninstall_desktopicon	
endif

comment "include custom post deinstall file"
set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
if not ($CustomPostDeinstall$ = "none")
	if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
		include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
	endif
endif

[Sub_uninstall_32]
Set $UninstallProgram$ = GetRegistryStringValue32("[" + $RegPathUninstall$ + "\%s%] UninstallString")
; Anführungsstriche in Registry-Pfad entfernen, da sie stören
Set $UninstallProgram$ = takeString (1, splitString ( $UninstallProgram$ , '"'))
Set $InstallDir$ = GetRegistryStringValue32("[" + $RegPathUninstall$ + "\%s%] InstallLocation")
if ($INST_architecture$ = "both")
	Set $ProductName$ = $PrettyName$ + " (32 bit)"
else
	Set $ProductName$ = $PrettyName$
endif			
Winbatch_uninstall
sub_check_exitcode
			
set $DesktopIcon$ = getProductProperty("desktopicon","no")
if $DesktopIcon$ = "yes"
	comment "Delete common desktop icon"
	Linkfolder_uninstall_desktopicon	
endif

comment "include custom post deinstall file"
set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
if not ($CustomPostDeinstall$ = "none")
	if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
		include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
	endif
endif

[Winbatch_uninstall]
"$UninstallProgram$" /VERYSILENT /NORESTART

[Linkfolder_uninstall_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $ProductName$

[Files_uninstall]
del -sf "$InstallDir$\"
